scn RHudScanScript

;; Thresholds
float fPrimary	; primary needs values
float fHealth	; limb health
float fArmor	; armor health
float fWeapon	; weapon health
float fWeight	; inventory weight

ref rHelmet
ref rArmor
float fCondition

ref rWeapon
ref rBinoculars
short sWeaponType
short sWeaponNeedsLoading


Begin GameMode
	set fPrimary to RHudHTSThreshold * RHudHTSPercentage/100
	set fHealth  to RHudLimbPercentage
	set fArmor   to RHudArmorPercentage
	set fWeapon  to RHudWeaponPercentage
	set fWeight  to RHudWeightPercentage

	;; Primary Needs Hud
	if (player.GetAV Dehydration >= fPrimary) || (player.GetAV Hunger >= fPrimary) || (player.GetAV SleepDeprevation >= fPrimary)
		set iHUD.pnhPrimaryOn to 1
		set iHUDFade.secondsToFadePNHPrimary to iHUDSettings.fadeSeconds
	endif


	;; Rads
	if (player.GetAV RadiationRads >= fPrimary)
		set iHUD.pnhRadsOn to 1
		set iHUDFade.secondsToFadePNHRads to iHUDSettings.fadeSeconds
	endif


	;; Inventory weight
	if ((player.GetAV InventoryWeight)/(player.GetAV CarryWeight)*100 >= fWeight)
		set iHUD.pnhEncumbOn to 1
		set iHUDFade.secondsToFadePNHEncumb to iHUDSettings.fadeSeconds
	endif


	;; Project Nevada's HUD - activates with vision mode usage or grenade selection
	if (PNxCVisionModeMain.state >= 10) || PNxCGrenadeHotkeyMain.selectTrap || PNxCGrenadeHotkeyMain.quickselectTrap
		set iHUD.pnOn to 1
		set iHUDFade.secondsToFadePN to iHUDSettings.fadeSeconds
	endif


	;; Weapon ammo and health
	set rWeapon to player.GetEquippedObject 5
	if rWeapon && (player.IsWeaponInList RepairBinoculars == 0) ; The Binoculars are One-handed Pistols(!), so weed them out
		set sWeaponType to player.GetWeaponType rWeapon
		;; Check current clip load for weapons that have clips and weapon health for all weapons
		if ((sWeaponType >= 3 && sWeaponType <= 9) && (GetClipSize rWeapon > GetPlayerCurrentAmmoRounds)) || (player.GetWeaponHealthPerc <= fWeapon)
				set iHUD.hudCNDOn to 1
				set iHUDFade.secondsToFadeCND to iHUDSettings.fadeSeconds
			endif
		endif
	endif

	;; Hud Extended's Health and Armor Hud
	;;
	;; 1. Check all of the health values
	if (player.GetAV perceptioncondition <= fHealth) || (player.GetAV leftattackcondition <= fHealth) || (player.GetAV endurancecondition <= fHealth) || (player.GetAV rightattackcondition <= fHealth) || (player.GetAV leftmobilitycondition <= fHealth) || (player.GetAV rightmobilitycondition <= fHealth)
		set HUDEShowHealth to 1
	else
		;; All armor and helmet values taken as-is from Imp's HUDEQuestScript

		;; 2. Check the armor
		set rArmor to player.getequippedobject 2
		if rArmor
			set fCondition to player.getequippedcurrenthealth 2
			set fCondition to fCondition / (gethealth rArmor) * 100
		else
			set fCondition to -1
		endif


		if (fCondition <= fArmor) 	; show if no armor => (fCondition == -1)
			set HUDEShowHealth to 1
		else
			;; 3. Check the helmet
			set rHelmet to player.getequippedobject 0							; head
			if rHelmet
				set fCondition to player.getequippedcurrenthealth 0
				set fCondition to fCondition / (gethealth rHelmet) * 100
			else
				set rHelmet to player.getequippedobject 9						; headband
				if rHelmet
					set fCondition to player.getequippedcurrenthealth 9
					set fCondition to fCondition / (gethealth rHelmet) * 100
				else
					set rHelmet to player.getequippedobject 10					; hat
					if rHelmet
						set fCondition to player.getequippedcurrenthealth 10
						set fCondition to fCondition / (gethealth rHelmet) * 100
					else
						set rHelmet to player.getequippedobject 11				; eyeglasses
						if rHelmet
							set fCondition to player.getequippedcurrenthealth 11
							set fCondition to fCondition / (gethealth rHelmet) * 100
						else
							set rHelmet to player.getequippedobject 14			; mask
							if rHelmet
								set fCondition to player.getequippedcurrenthealth 14
								set fCondition to fCondition / (gethealth rHelmet) * 100
							else
								set fCondition to -1
							endif
						endif
					endif
				endif
			endif

			if (fCondition <= fArmor) 	; show if no helmet => (fCondition == -1)
				set HUDEShowHealth to 1
			else
				set HUDEShowHealth to 0
			endif
		endif
	endif
End